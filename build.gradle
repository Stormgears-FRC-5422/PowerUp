import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
	id "java"
	id "eclipse"
	id "idea"
	id "org.jetbrains.kotlin.jvm" version "1.2.30"
	id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
}

group 'org.stormgears.powerup'
version '1.0-SNAPSHOT'

def TEAM = 5422
def ROBOT_CLASS = "org.stormgears.powerup.Robot"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

deploy {
	targets {
		target("roborio", RoboRIO) {
			team = TEAM
		}
	}
	artifacts {
		artifact('frcJava', FRCJavaArtifact) {
			targets << "roborio"
		}
	}
}

dependencies {
	compile wpilib()
	compile navx()
	compile ctre()

	compile group: 'org.apache.commons', name: 'commons-text', version: '1.2'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
	compile group: 'com.lmax', name: 'disruptor', version: '3.3.8'

	compile group: 'org.jetbrains', name: 'annotations', version: '15.0'

	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

	compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.3'

	testCompile group: 'junit', name: 'junit', version: '4.12'
}

kotlin {
	experimental {
		coroutines 'enable'
	}
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

configurations {
	jar.archiveName = 'FRCUserProgram.jar'
}

jar {
	from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

telemetry {
	reportTelemetry = false
}
